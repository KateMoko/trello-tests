plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

repositories {
    mavenCentral()
}

group = 'com.katemoko'
version = '1.0-SNAPSHOT'

def jUnitVersion = "5.9.3",
    slf4jVersion = "2.0.7",
    restAssuredVersion = "5.3.1",
    allureVersion = "2.23.0",
    selenideVersion = "6.16.1",
    ownerVersion = "1.0.12",
    jsoupVersion = "1.16.1",
    fakerVersion = "2.0.1"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$jUnitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.jsoup:jsoup:$jsoupVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "net.datafaker:datafaker:$fakerVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task ui_test(type: Test) {
    useJUnitPlatform {
        includeTags("ui")
    }
}

task api_test(type: Test) {
    useJUnitPlatform {
        includeTags("api")
    }
}