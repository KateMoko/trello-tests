plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.freefair.lombok' version '6.6.3'
}

repositories {
    mavenCentral()
}

group = 'com.katemoko'
version = '1.0-SNAPSHOT'

def jUnitVersion = "5.10.2",
    slf4jVersion = "2.0.12",
    restAssuredVersion = "5.4.0",
    jacksonVersion = "2.16.1",
    allureVersion = "2.25.0",
    selenideVersion = "7.1.0",
    ownerVersion = "1.0.12",
    fakerVersion = "1.0.2"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$jUnitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "com.github.javafaker:javafaker:$fakerVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion")
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task ui_test(type: Test) {
    useJUnitPlatform {
        includeTags("ui")
    }
}

task api_test(type: Test) {
    useJUnitPlatform {
        includeTags("api")
    }
}